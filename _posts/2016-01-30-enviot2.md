---
layout: post
title:  "Temperature and Humidity IoT"
date:   2016-01-30 21:23:21
categories: electronics
---
Since first prototyping the environmental IoT device i've changed my mind on a
few things. First is that having a temperature sensor attached to a GPS is
kind of bulky especially if you want to have it connected over WiFi. instead
I'm splitting it out into two separate projects. First is the temperature and
humidity sensor, second is the environmental box. The temp sensor will be a very
low cost and wireless device that sends its data to an MQTT broker. The
environmental box will not be wifi connected and it's purpose will be to mostly
look cool.

This post details the design and process of the WiFi temperature and humidity
sensor.

Prototyping will be completed on a breadboard and designed using
[Fritzing](http://fritzing.org/). The final PCB design will be completed in
[EagleCad](http://www.cadsoftusa.com/).

I had originally started out with the ESP8266 ESP-01 but was unable to have it
connect to my router. While I'm sure with a bit of troubleshooting I would have
succeeded in establishing connectivity I was not convinced it would be the
easiest module to attach to a PCB. Since this project ultimately aims to have
a PCB version the decision was easy. Plus I get more pins and it looks cooler.

**Breadboarding**
I was disappointed to see that upon plugging the module into the breadboard
the breakout board pins were at each extreme thus not allowing me to plug
any pins in like is typically done. As a quick fix I forced some longer headers
into the board and then the breakout board into the headers.

![breadboard]({{ site.url }}/media/images/201601_enviot2/IMAG0099.jpg)

{% highlight c++ %}
/***************************************************
  Adafruit MQTT Library ESP8266 Example

  Must use ESP8266 Arduino from:
    https://github.com/esp8266/Arduino

  Works great with Adafruit's Huzzah ESP board:
  ----> https://www.adafruit.com/product/2471

  Adafruit invests time and resources providing this open source code,
  please support Adafruit and open-source hardware by purchasing
  products from Adafruit!

  Written by Tony DiCola for Adafruit Industries.
  MIT license, all text above must be included in any redistribution
 ****************************************************/
#include <ESP8266WiFi.h>
#include <WiFiClient.h>
#include "Adafruit_MQTT.h"
#include "Adafruit_MQTT_Client.h"
#include <DHT.h>
#define DHTTYPE DHT22
#define DHTPIN  2

// Initialize DHT sensor
// NOTE: For working with a faster than ATmega328p 16 MHz Arduino chip, like an ESP8266,
// you need to increase the threshold for cycle counts considered a 1 or 0.
// You can do this by passing a 3rd parameter for this threshold.  It's a bit
// of fiddling to find the right value, but in general the faster the CPU the
// higher the value.  The default for a 16mhz AVR is a value of 6.  For an
// Arduino Due that runs at 84mhz a value of 30 works.
// This is for the ESP8266 processor on ESP-01
DHT dht(DHTPIN, DHTTYPE, 11); // 11 works fine for ESP8266

float humidity, temp_f;  // Values read from sensor

/************************* WiFi Access Point *********************************/

#define WLAN_SSID       "Poppins"
#define WLAN_PASS       "#####"

/************************* Adafruit.io Setup *********************************/

#define AIO_SERVER      "io.adafruit.com"
#define AIO_SERVERPORT  1883
#define AIO_USERNAME    "cadamei"
#define AIO_KEY         "5027a55ee0af2f7342a5d71dab0cc96281593264"

/************ Global State (you don't need to change this!) ******************/

// Create an ESP8266 WiFiClient class to connect to the MQTT server.
WiFiClient client;

// Store the MQTT server, username, and password in flash memory.
// This is required for using the Adafruit MQTT library.
const char MQTT_SERVER[] PROGMEM    = AIO_SERVER;
const char MQTT_USERNAME[] PROGMEM  = AIO_USERNAME;
const char MQTT_PASSWORD[] PROGMEM  = AIO_KEY;

// Setup the MQTT client class by passing in the WiFi client and MQTT server and login details.
Adafruit_MQTT_Client mqtt(&client, MQTT_SERVER, AIO_SERVERPORT, MQTT_USERNAME, MQTT_PASSWORD);

/****************************** Feeds ***************************************/

// Setup a feed called 'photocell' for publishing.
// Notice MQTT paths for AIO follow the form: <username>/feeds/<feedname>
const char TEMP_FEED[] PROGMEM = AIO_USERNAME "/feeds/temperature";
Adafruit_MQTT_Publish temp = Adafruit_MQTT_Publish(&mqtt, TEMP_FEED);

// Setup a feed called 'photocell' for publishing.
// Notice MQTT paths for AIO follow the form: <username>/feeds/<feedname>
const char HUM_FEED[] PROGMEM = AIO_USERNAME "/feeds/humidity";
Adafruit_MQTT_Publish hum = Adafruit_MQTT_Publish(&mqtt, HUM_FEED);

// Setup a feed called 'onoff' for subscribing to changes.
const char ONOFF_FEED[] PROGMEM = AIO_USERNAME "/feeds/onoff";
Adafruit_MQTT_Subscribe onoffbutton = Adafruit_MQTT_Subscribe(&mqtt, ONOFF_FEED);

/*************************** Sketch Code ************************************/

// Bug workaround for Arduino 1.6.6, it seems to need a function declaration
// for some reason (only affects ESP8266, likely an arduino-builder bug).
void MQTT_connect();

void setup() {
  Serial.begin(9600);
  dht.begin();           // initialize temperature sensor
  delay(10);

  Serial.println(F("Adafruit MQTT demo"));

  // Connect to WiFi access point.
  Serial.println(); Serial.println();
  Serial.print("Connecting to ");
  Serial.println(WLAN_SSID);

  WiFi.begin(WLAN_SSID, WLAN_PASS);
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println();

  Serial.println("WiFi connected");
  Serial.println("IP address: "); Serial.println(WiFi.localIP());

  // Setup MQTT subscription for onoff feed.
  mqtt.subscribe(&onoffbutton);
}

void gettemperature() {
    humidity = dht.readHumidity();          // Read humidity (percent)
    temp_f = dht.readTemperature();     // Read temperature as Fahrenheit
}

void loop() {
  // Ensure the connection to the MQTT server is alive (this will make the first
  // connection and automatically reconnect when disconnected).  See the MQTT_connect
  // function definition further below.
  MQTT_connect();

  // this is our 'wait for incoming subscription packets' busy subloop
  Adafruit_MQTT_Subscribe *subscription;
  while ((subscription = mqtt.readSubscription(1000))) {
    if (subscription == &onoffbutton) {
      Serial.print(F("Got: "));
      Serial.println((char *)onoffbutton.lastread);
    }
  }

  // Now we can publish stuff!
  Serial.print(F("\nSending temp val "));
  gettemperature();       // read sensor
  Serial.print(temp_f);
  Serial.print("...");
  if (! temp.publish(temp_f)) {
    Serial.println(F("Failed"));
  } else {
    Serial.println(F("OK!"));
  }

    // Now we can publish stuff!
  Serial.print(F("\nSending humidity val "));
  gettemperature();       // read sensor
  Serial.print(humidity);
  Serial.print("...");
  if (! hum.publish(humidity)) {
    Serial.println(F("Failed"));
  } else {
    Serial.println(F("OK!"));
  }

  // ping the server to keep the mqtt connection alive
  if(! mqtt.ping()) {
    mqtt.disconnect();
  }

  delay(1000);

}

// Function to connect and reconnect as necessary to the MQTT server.
// Should be called in the loop function and it will take care if connecting.
void MQTT_connect() {
  int8_t ret;

  // Stop if already connected.
  if (mqtt.connected()) {
    return;
  }

  Serial.print("Connecting to MQTT... ");

  while ((ret = mqtt.connect()) != 0) { // connect will return 0 for connected
       Serial.println(mqtt.connectErrorString(ret));
       Serial.println("Retrying MQTT connection in 5 seconds...");
       mqtt.disconnect();
       delay(5000);  // wait 5 seconds
  }
  Serial.println("MQTT Connected!");
}
{% endhighlight %}

When cabled and powered on use Putty to monitor the serial port and the output should be similar to the below:

>provided altitude: 31.00 meters    
temperature: 29.22 deg C    
absolute pressure: 1012.68 hPa    
relative (sea-level) pressure: 1016.41 hPa

The final set-up
![Final Setup]({{ site.url }}/media/images/201511_enviot/2015-11-28 21.22.08.jpg)

All In One
===

I was going to individually connect all the modules to the Arduino and execute their code one at a time but I got bored of that and decided to connect everything together as it will be in it's final state.

Here's the setup
![Final Wiring]({{ site.url }}/media/images/201511_enviot/TiTe2_bb.png)

And here's what it looks like in real life
![Real Life]({{ site.url }}/media/images/201511_enviot/TiTe2_RL.jpg)

As you can see I replaced the Uno with a Mega du to the lack of space once all the libraries were included.

I've since run the example code from the Arduino Adafruit library and confirmed I can connect to the wireless using the C3300 wireless module. One mistake I made when trying to connect was not having 802.11b/g enabled on my router. This meant I had no obvious errors display on the serial monitor of the Arduino so it took me a while to realise why it wasn't connecting. After enabling 802.11b/g it connected and I was able to run the sketches. My favourite was the mini web server displaying *Hello World!* in my web browser served from the Arduino.

Since I know the Arduino can successfully read data from all the modules in its current connected state I will move onto designing the software. Arduino Uno has limited resources so be looking at designing the software to send from the Arduino to a central location that has enough capacity to display the data over the Internet. This means the it will only need to maintain one connection to the cloud service and the cloud service to many devices.

The first cloud service I found was [Freeboard.io](https://www.freeboard.io) so I'll be going with that for my dashboard. I had planned on creating my own web enabled dashboard but it's probably better I take baby steps and perhaps learn from the model of Freeboard.
